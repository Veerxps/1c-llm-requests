
Функция ЗапросыМетодВыполнитьЗапрос(Запрос)
	ВходящийJSON = Запрос.ПолучитьТелоКакСтроку();

	Если ПустаяСтрока(ВходящийJSON) Тогда
		Возврат Ошибка400("Пустое тело запроса.");
	КонецЕсли;

	Попытка
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(ВходящийJSON);
		Данные = ПрочитатьJSON(Чтение);
	Исключение
		Возврат Ошибка400("Некорректный JSON.");
	КонецПопытки;

	Если Данные = Неопределено Или ТипЗнч(Данные) <> Тип("Структура") Тогда
		Возврат Ошибка400("Ожидается JSON объект.");
	КонецЕсли;

	Если НЕ Данные.Свойство("query") Тогда
		Возврат Ошибка400("Отсутствует обязательное поле query.");
	КонецЕсли;

	ТекстЗапроса = Данные.query;
	Если ТипЗнч(ТекстЗапроса) <> Тип("Строка") Или ПустаяСтрока(ТекстЗапроса) Тогда
		Возврат Ошибка400("Поле query должно быть непустой строкой.");
	КонецЕсли;

	Параметры = Неопределено;
	Если Данные.Свойство("params") Тогда
		Параметры = Данные.params;
		Если ТипЗнч(Параметры) <> Тип("Массив") Тогда
			Возврат Ошибка400("Поле params должно быть массивом.");
		КонецЕсли;

		Для каждого Элемент Из Параметры Цикл
			Если ТипЗнч(Элемент) <> Тип("Структура") Тогда
				Возврат Ошибка400("Элементы params должны быть объектами.");
			КонецЕсли;
			Если НЕ Элемент.Свойство("name") Тогда
				Возврат Ошибка400("В params отсутствует поле name.");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	//ТекстОтвета = "Запрос: " + ТекстЗапроса;
	//Если Параметры <> Неопределено Тогда
	//	ТекстОтвета = ТекстОтвета + Символы.ПС + "Параметры: " + ФорматПараметровМассив(Параметры);
	//Иначе
	//	ТекстОтвета = ТекстОтвета + Символы.ПС + "Параметры: нет";
	//КонецЕсли;
	
	Таблица = ПолучитьТЗПоЗапросу(ТекстЗапроса, Параметры);
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "text/tab-separated-values; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ТаблицаЗначенийВTSV(Таблица));

	Возврат Ответ;
КонецФункции


Функция ЗапросыПроверка(Запрос)
	Ответ = Новый HTTPСервисОтвет(200); 
	Ответ.УстановитьТелоИзСтроки("{""result"":""ok""}");
	Ответ.Заголовки.Вставить("Content-type", "application/json");
	Возврат Ответ;
КонецФункции

Функция ТаблицаЗначенийВTSV(ТЗ) Экспорт
	Если ТЗ = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	СтрокиTSV = Новый Массив;

	// Заголовок
	МассивКолонок = Новый Массив;
	Для каждого Колонка Из ТЗ.Колонки Цикл
		МассивКолонок.Добавить(Экранировать(СокрЛП(Колонка.Имя)));
	КонецЦикла;
	СтрокиTSV.Добавить(СтрСоединить(МассивКолонок, Символы.Таб));

	// Данные
	Для каждого СтрокаТаблицы Из ТЗ Цикл
		СтрокаМассива = Новый Массив;
		Для каждого Колонка Из ТЗ.Колонки Цикл
			Значение = СтрокаТаблицы[Колонка.Имя];
			СтрокаМассива.Добавить(Экранировать(Строка(Значение)));
		КонецЦикла;
		СтрокиTSV.Добавить(СтрСоединить(СтрокаМассива, Символы.Таб));
	КонецЦикла;

	Возврат СтрСоединить(СтрокиTSV, Символы.ПС);
КонецФункции

Функция Экранировать(СтрокаЗначение) Экспорт
	Если СтрокаЗначение = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	Результат = СтрЗаменить(СтрокаЗначение, Символы.Таб, " ");
	Результат = СтрЗаменить(Результат, ";", ",");
	Результат = СтрЗаменить(Результат, Символы.ВК, " ");
	Результат = СтрЗаменить(Результат, Символы.ПС, " ");

	Возврат Результат;
КонецФункции

Функция ПолучитьТЗПоЗапросу(ТекстЗапроса, Параметры) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Если Параметры <> Неопределено Тогда
		Для каждого Элемент Из Параметры Цикл
			ИмяПараметра = Элемент.name;
			ЗначениеПараметра = ?(Элемент.Свойство("value"), Элемент.value, Неопределено);
			Запрос.УстановитьПараметр(ИмяПараметра, ЗначениеПараметра);
		КонецЦикла;
	КонецЕсли;

	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция Ошибка400(Сообщение) Экспорт
	Ответ = Новый HTTPСервисОтвет(400);
	Ответ.Заголовки.Вставить("Content-Type", "text/plain; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(Сообщение);
	Возврат Ответ;
КонецФункции

Функция ФорматПараметровМассив(Параметры) Экспорт
	Части = Новый Массив;

	Для каждого Элемент Из Параметры Цикл
		Имя = Элемент.name;
		Значение = ?(Элемент.Свойство("value"), Элемент.value, "");
		Части.Добавить(Строка(Имя) + "=" + Строка(Значение));
	КонецЦикла;

	Возврат СтрСоединить(Части, ", ");
КонецФункции
